#version 460
#extension GL_ARB_gpu_shader_int64 : require
#extension GL_ARB_shader_clock : require
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_ray_tracing : require

#include "lib/Heatmap.glsl"
#include "lib/Random.glsl"
#include "lib/RayTracingCommons.glsl"
#include "lib/UniformBufferObject.glsl"
#include "lib/Material.glsl"

#define MIN_BOUNCES 4

layout(binding = AS_BIND, set = 0) uniform accelerationStructureEXT Scene;
layout(binding = ACC_BIND, set = 1, rgba32f) uniform image2D AccumulationImage;
layout(binding = STORAGE_BIND, set = 1, rgba8) uniform image2D OutputImage;
layout(binding = UNIFORM_BIND, set = 0) readonly uniform UniformBufferObjectStruct { UniformBufferObject Camera; };

layout(location = 0) rayPayloadEXT RayPayload Ray;


void main()
{
    const uint64_t clock = Camera.mapping == HEAT ? clockARB() : 0;

    // Initialise separate random seeds for the pixel and the rays.
    // - pixel: we want the same random seed for each pixel to get a homogeneous anti-aliasing.
    // - ray: we want a noisy random seed, different for each pixel.
    uint pixelRandomSeed = Camera.RandomSeed;
    Ray.RandomSeed = InitRandomSeed(InitRandomSeed(gl_LaunchIDEXT.x, gl_LaunchIDEXT.y), uint(clockARB()));
    RngStateType rngState = initRNG(uvec2(gl_LaunchIDEXT), uvec2(gl_LaunchSizeEXT.xy), uint(clockARB()));
    vec3 radiance = vec3(0);
//    Ray.instance_id = 0xFFFFFFFF;
    // Accumulate all the rays for this pixels.
    for (uint s = 0; s < Camera.NumberOfSamples; ++s)
    {
        //if (Camera.NumberOfSamples != Camera.TotalNumberOfSamples) break;
        const vec2 pixel = vec2(gl_LaunchIDEXT.x + RandomFloat(pixelRandomSeed), gl_LaunchIDEXT.y + RandomFloat(pixelRandomSeed));
        const vec2 uv = (pixel / gl_LaunchSizeEXT.xy) * 2.0 - 1.0;

        vec2 offset = Camera.Aperture / 2 * RandomInUnitDisk(Ray.RandomSeed);
        vec4 origin = Camera.ModelViewInverse * vec4(offset, 0, 1);
        vec4 target = Camera.ProjectionInverse * (vec4(uv.x, uv.y, 1, 1));
        vec4 direction = Camera.ModelViewInverse * vec4(normalize(target.xyz * Camera.FocusDistance - vec3(offset, 0)), 0);
        vec3 throughput = vec3(1);

        // Ray scatters are handled in this loop. There are no recursive traceRayEXT() calls in other shaders.
        for (uint b = 0; b <= Camera.NumberOfBounces; b++)
        {
            // If we've exceeded the ray bounce limit without hitting a light source, no light is gathered.
            // Light emitting materials never scatter in this implementation, allowing us to make this logical shortcut.
            if (b == Camera.NumberOfBounces)
            {
                throughput = vec3(0, 0, 0);
                break;
            }



            traceRayEXT(
            Scene, gl_RayFlagsOpaqueEXT, 0xff,
            0 /*sbtRecordOffset*/, 0 /*sbtRecordStride*/, 0 /*missIndex*/,
            origin.xyz, tMin, direction.xyz, tMax, 0 /*payload*/);

            const vec3 hitColor = Ray.hitValue;
            const float t = Ray.t;
            const bool isScattered = Ray.needScatter;

            radiance += throughput * Ray.emittance;
            Ray.emittance = vec3(0.);

            if (b > MIN_BOUNCES) {
                float rrProbability = clamp(luminance(throughput), 0.01, 0.95);
                float prop = rand(rngState);
                if (rrProbability < prop) {
                    break;
                }
                else {
                    throughput /= rrProbability;
                }
            }

            throughput *= hitColor;
            // Trace missed, or end of trace.
            if (t < 0 )
            {
                break;
            }
            if (!isScattered) {
                break;
            }
            // Trace hit.
            origin = vec4(Ray.hitPoint, 1.);
            direction = vec4(Ray.scatterDirection, 0);
        }

        radiance += throughput;
    }

    const bool accumulate = Camera.NumberOfSamples != Camera.TotalNumberOfSamples;
    const vec3 accumulatedColor = (accumulate ? imageLoad(AccumulationImage, ivec2(gl_LaunchIDEXT.xy)) : vec4(0)).rgb + radiance;

    radiance = accumulatedColor / Camera.TotalNumberOfSamples;

    // Apply raytracing-in-one-weekend gamma correction.
    radiance = sqrt(radiance);

    switch (Camera.mapping)
    {
        case HEAT:
            const uint64_t deltaTime = clockARB() - clock;
            const float heatmapScale = 1000000.0f * Camera.HeatmapScale * Camera.HeatmapScale;
            const float deltaTimeScaled = clamp(float(deltaTime) / heatmapScale, 0.0f, 1.0f);
            radiance = heatmap(deltaTimeScaled);
            break;
        case DISTANCE:
            radiance = vec3(min((Camera.HeatmapScale - max(Ray.t, tMin)) / Camera.HeatmapScale, 1));
            break;
        case RENDER:
            break;
    }
    imageStore(AccumulationImage, ivec2(gl_LaunchIDEXT.xy), vec4(accumulatedColor, 0));
    imageStore(OutputImage, ivec2(gl_LaunchIDEXT.xy), vec4(radiance, 0));
}
